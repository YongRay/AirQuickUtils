apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = getProperty('projectGroupName').toString()
version = getProperty('projectVersionName').toString()


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.2'
    testCompile 'junit:junit:4.12'
}


task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
    group = 'build'
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
    group = 'build'
}

task generateJavadocJar(type: Jar) {
    from generateJavadoc
    classifier 'javadoc'
    group = 'build'
}

publishing {
    publications {
        AirQuickUtils(MavenPublication) {
            artifact "$buildDir/outputs/aar/AirQuickUtils-release.aar"
            artifact generateJavadocJar
            artifact generateSourcesJar

            groupId getProperty('projectGroupName').toString()
            artifactId "AirQuickUtils"
            version getProperty('projectVersionName').toString()

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = getProperty('bintrayUser').toString()
    key = getProperty('bintrayApiKey').toString()
    publications = ['AirQuickUtils']
    pkg {
        repo = 'maven'
        name = 'AirQuickUtils'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/yongbeam/AirQuickUtils'
        publish = true
        publicDownloadNumbers = true

        version {
            name = getProperty('projectVersionName').toString()
            desc = 'Android utility classes.'
            released  = new Date()
            vcsTag = 'release_' + getProperty('projectVersionName').toString()
        }
    }
}